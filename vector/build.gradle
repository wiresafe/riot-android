apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "im.vector"
        minSdkVersion 16
        targetSdkVersion 26
        // use the version code
        versionCode rootProject.ext.versionCodeProp
        versionName rootProject.ext.versionNameProp

        // custom values
        resValue "string", "bug_report_url", "https://riot.im/bugreports/submit"

        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        packagingOptions {
            // The project react-native does not provide 64-bit binaries at the
            // time of this writing. Unfortunately, packaging any 64-bit
            // binaries into the .apk will crash the app at runtime on 64-bit
            // platforms.
            exclude "lib/x86_64/libjingle_peerconnection_so.so"
            exclude "lib/arm64-v8a/libjingle_peerconnection_so.so"
        }

        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
//            storeFile file(project.hasProperty("RELEASE_STORE_FILE") ? RELEASE_STORE_FILE : "/dummy/path")
//            storePassword project.hasProperty("RELEASE_STORE_PASSWORD") ? RELEASE_STORE_PASSWORD : "dummy password"
//            keyAlias project.hasProperty("RELEASE_KEY_ALIAS") ? RELEASE_KEY_ALIAS : "dummy alias"
//            keyPassword project.hasProperty("RELEASE_KEY_PASSWORD") ? RELEASE_KEY_PASSWORD : "dummy password"
        }
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            resValue "string", "git_branch_name", "\"${gitBranchName()}\""
            minifyEnabled false
        }

        release {
            signingConfig signingConfigs.release

            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            resValue "string", "git_branch_name", "\"${gitBranchName()}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        matrixorg.initWith(buildTypes.release)
        matrixorg {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        app {
            applicationId "im.vector.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "true"
            resValue "string", "allow_ga_use", "true"
            resValue "string", "short_flavor_description", "G"
            resValue "string", "flavor_description", "GooglePlay"
        }

        appfdroid {
            applicationId "im.vector.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "false"
            resValue "string", "allow_ga_use", "false"
            resValue "string", "short_flavor_description", "F"
            resValue "string", "flavor_description", "FDroid"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingTranslation'
    }

    repositories {
        flatDir {
            dir 'libs'
        }

        maven {
            url "https://s3.amazonaws.com/repo.commonsware.com"
        }

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

def gitRevisionDate() {
    def cmd = "git show -s --format=%ci HEAD^{commit}"
    return cmd.execute().text.trim()
}

def gitBranchName() {
    def cmd = "git name-rev --name-only HEAD"
    return cmd.execute().text.trim()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:multidex:1.0.0'

    compile 'me.leolin:ShortcutBadger:1.1.2@aar'

    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'

    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.+'


    compile 'com.googlecode.libphonenumber:libphonenumber:8.2.0'

    /************* Matrix SDK management **************/
    // use the matrix SDK as external lib
    compile(name: 'matrix-sdk', ext: 'aar')
    compile(name: 'olm-sdk', ext: 'aar')

    /************* jitsi **************/
    compile 'javax.inject:javax.inject:1'
    compile 'com.facebook.fbui.textlayoutbuilder:textlayoutbuilder:1.0.0'
    compile 'com.facebook.fresco:fresco:0.11.0'
    compile 'com.facebook.fresco:imagepipeline-okhttp3:0.11.0'
    compile 'com.facebook.soloader:soloader:0.1.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-ws:3.4.1'
    compile 'com.squareup.okio:okio:1.9.0'
    compile 'org.webkit:android-jsc:r174650'

    compile(name: 'jitsi-sdk', ext: 'aar')
    compile(name: 'react-native-background-timer', ext: 'aar')
    compile(name: 'react-native-fetch-blob', ext: 'aar')
    compile(name: 'react-native-immersive', ext: 'aar')
    compile(name: 'react-native-keep-awake', ext: 'aar')
    compile(name: 'react-native-vector-icons', ext: 'aar')
    compile(name: 'react-native-webrtc', ext: 'aar')
    compile(name: 'react-native', ext: 'aar')
    compile(name: 'material-spinner-1.2.1', ext: 'aar')

    // use the matrix SDK as a part of the project
    // you have to uncomment some lines in settings.gradle
    //compile project(':matrix-sdk')

    /************* flavors management **************/

    // app flavor only
    appCompile 'com.google.firebase:firebase-core:11.6.0'
    appCompile 'com.google.firebase:firebase-messaging:11.6.0'
    compile 'com.google.firebase:firebase-database:11.6.0'
    appCompile 'com.google.android.gms:play-services-analytics:11.6.0'

    appCompile 'com.github.deano2390:MaterialShowcaseView:1.2.0@aar'

    // fdroid flavor only

    compile 'com.google.firebase:firebase-auth:11.6.0'
    compile 'com.google.android.gms:play-services-auth:11.6.0'

    compile 'com.firebaseui:firebase-ui-auth:3.0.0'
}


if (!getGradle().getStartParameter().getTaskRequests().toString().contains("fdroid")) {
    apply plugin: 'com.google.gms.google-services'
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'